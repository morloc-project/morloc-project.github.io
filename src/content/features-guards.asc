=== Guards

Guards provide conditional branching within function definitions. Each guard
clause begins with `?` followed by a condition and a result expression, with a
`:` default that is always required as the final case:

[source, morloc]
----
abs :: Int -> Int
abs x
  ? x >= 0 = x
  : neg x
----

Guards are evaluated lazily from top to bottom. The first condition that
evaluates to true determines the result; remaining guards are not evaluated. The
`:` default always terminates the guard chain, ensuring exhaustiveness.

Guards work naturally with multiple parameters:

[source, morloc]
----
clamp :: Int -> Int -> Int -> Int
clamp lo hi x
  ? x < lo = lo
  ? x > hi = hi
  : x
----

Guards can be combined with `where` clauses to define local bindings used in the
conditions and result expressions:

[source, morloc]
----
classify :: Int -> Str
classify x
  ? x > big = "big"
  ? x > small = "medium"
  : "small"
  where
    big = 100
    small = 10
----

Guards may appear inside `let` bindings:

[source, morloc]
----
absLet :: Int -> Int
absLet x =
  let result ? x >= 0 = x
             : neg x
  in result
----

Guards can also be used as inline expressions anywhere a value is expected,
enclosed in parentheses:

[source, morloc]
----
classify :: Int -> Str
classify x = (? x > 100 = "big" ? x > 10 = "medium" : "small")
----
